( GUI Picture )

%RTN { JMP2r }
%LOAD-IMAGE { .File/length DEO2 .File/name DEO2 ;image .File/load DEO2 }

( devices )

|00 @System [ &vector $2 &pad $6 &r $2 &g $2 &b $2 ]
|20 @Screen [ &vector $2 &width $2 &height $2 &pad $2 &x $2 &y $2 &addr $2 &pixel $1 &sprite $1 ]
|a0 @File [ &vector $2 &success $2 &offset-hs $2 &offset-ls $2 &name $2 &length $2 &load $2 &save $2 ]

( variables )

|0000

@position
	&x $2 &y $2
@size
	&width $2 &height $2

( program )

|0100

	( theme ) 
	#072f .System/r DEO2 
	#072f .System/g DEO2 
	#072f .System/b DEO2

	( background ) ;checker-icn #02 ;cover-pattern JSR2

	( load ) ;icn1-path #1800 LOAD-IMAGE
	( draw ) #0008 #0008 #0100 #00c0 ;image #01 ;draw-icn JSR2
	( load ) ;icn2-path #0800 LOAD-IMAGE
	( draw ) #0010 #00a8 #0080 #0080 ;image #03 ;draw-icn JSR2
	( load ) ;chr1-path #4000 LOAD-IMAGE
	( draw ) #00e8 #0080 #0100 #0080 ;image #81 ;draw-chr JSR2
	( load ) ;chr2-path #0900 LOAD-IMAGE
	( draw ) #0068 #0098 #0060 #0060 ;image #85 ;draw-chr JSR2

BRK

@draw-icn ( x* y* width* height* addr* color  -- )
	
	( load ) STH .Screen/addr DEO2 .size/height STZ2 .size/width STZ2 .position/y STZ2 .position/x STZ2
	.size/height LDZ2 #0000 
	&ver
		( save ) DUP2 .position/y LDZ2 ADD2 .Screen/y DEO2
		.size/width LDZ2 #0000
		&hor
			( save ) DUP2 .position/x LDZ2 ADD2 .Screen/x DEO2
			( draw ) STHkr .Screen/sprite DEO
			( incr ) .Screen/addr DEI2 #0008 ADD2 .Screen/addr DEO2
			#0008 ADD2 GTH2k ,&hor JCN
		POP2 POP2
		#0008 ADD2 GTH2k ,&ver JCN
	POP2 POP2
	POPr

RTN

@draw-chr ( x* y* width* height* addr* color -- )
	
	( load ) STH .Screen/addr DEO2 .size/height STZ2 .size/width STZ2 .position/y STZ2 .position/x STZ2
	.size/height LDZ2 #0000
	&ver
		( save ) DUP2 .position/y LDZ2 ADD2 .Screen/y DEO2
		.size/width LDZ2 #0000
		&hor
			( save ) DUP2 .position/x LDZ2 ADD2 .Screen/x DEO2
			( draw ) STHkr .Screen/sprite DEO
			( incr ) .Screen/addr DEI2 #0010 ADD2 .Screen/addr DEO2
			#0008 ADD2 GTH2k ,&hor JCN
		POP2 POP2
		#0008 ADD2 GTH2k ,&ver JCN
	POP2 POP2
	POPr

RTN

@cover-pattern ( addr* color -- )
	
	( load ) STH .Screen/addr DEO2
	.Screen/height DEI2 #0000
	&ver
		( save ) DUP2 .Screen/y DEO2
		.Screen/width DEI2 #0000
		&hor
			( save ) DUP2 .Screen/x DEO2
			( draw ) STHkr .Screen/sprite DEO
			#0008 ADD2 GTH2k ,&hor JCN
		POP2 POP2
		#0008 ADD2 GTH2k ,&ver JCN
	POP2 POP2
	POPr

RTN

@checker-icn f0f0 f0f0 0f0f 0f0f
@icn1-path   "projects/pictures/ergo100x0c0.bit 00
@icn2-path   "projects/pictures/dafu80x80.bit 00
@chr1-path   "projects/pictures/pc98.chr 00
@chr2-path   "projects/pictures/felix0cx0c.chr 00

@image [ ]